apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'appengine'

def appId = "socialfridge"
def appVersion = "0.1"

def appengineVersion = "1.9.1"

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "com.google.appengine:gradle-appengine-plugin:1.8.9"
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    appengineSdk "com.google.appengine:appengine-java-sdk:${appengineVersion}"

    compile "com.google.guava:guava:16.+"

    compile "com.googlecode.objectify:objectify:4.+"

    // App Engine
    compile "javax.servlet:servlet-api:2.5"
    compile "org.apache.geronimo.specs:geronimo-jpa_2.0_spec:1.1"
    compile "com.google.code.findbugs:jsr305:2.0.3"
    compile "javax.inject:javax.inject:1"

    compile "org.datanucleus:datanucleus-core:3.1.3"
    compile "org.datanucleus:datanucleus-api-jpa:3.1.3"
    compile "com.google.appengine.orm:datanucleus-appengine:2.1.2"

    compile "com.ganyo:gcm-server:1.0.2"

    compile "com.google.appengine:appengine-api-1.0-sdk:${appengineVersion}"
    compile "com.google.appengine:appengine-endpoints:${appengineVersion}"

    testCompile "com.google.appengine:appengine-testing:${appengineVersion}"
    testCompile "com.google.appengine:appengine-api-stubs:${appengineVersion}"

    // Facebook
    compile "com.restfb:restfb:1.6.+"
}

sourceSets {
    main {
        java {
            srcDir "src/main/java"
        }
        output.classesDir = "src/main/webapp/WEB-INF/classes"
    }
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
}

appengine {
    httpAddress = "0.0.0.0"
    jvmFlags = ['-d64', '-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=8889', '-Xmx1024m', '-XX:MaxPermSize=512m']
    downloadSdk = true

    appcfg {
        update {
            useJava7 = true
        }

        enhancerApi = "jpa"
        enhancerVersion = "v2"

        logs {
            severity = 1
            numDays = 1
            outputFile = file("${appId}.log")
        }

        app {
            id = "${appId}"
            version = "${appVersion}"
        }

        endpoints {
            getDiscoveryDocsOnBuild = true
            getClientLibsOnBuild = true
        }
    }
}

/*
 * Dirty fix: run enhancer before war
 */
def appengineEnhanceTask = project.tasks.appengineEnhance
def javaClassesTask = project.tasks.getByName(JavaPlugin.CLASSES_TASK_NAME)
def warTask = project.tasks.getByName(WarPlugin.WAR_TASK_NAME)

warTask.dependsOn(appengineEnhanceTask)
appengineEnhanceTask.dependsOn(javaClassesTask)
